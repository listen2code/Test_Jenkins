apply plugin: 'com.android.application'

android {
    signingConfigs {
        testjenkins {
            keyAlias 'testjenkins'
            keyPassword 'testjenkins'
            storeFile file('src/key/test_jenkins.jks')
            storePassword 'testjenkins'
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.0"
    defaultConfig {
        applicationId "com.listen.test_jenkins"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.testjenkins
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def apk = output.outputFile
                    if (apk != null && apk.name.endsWith('.apk')) {
                        def fileName = "lisong_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(apk.parent, fileName)
                    }
                }
            }
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.testjenkins
        }
    }

    // 多渠道
    productFlavors {
        Baidu {}
        WanDouJia {}
        Xiaomi {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [CHANNEL: name]
    }
    // 多渠道

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:23.4.0'
    testCompile 'junit:junit:4.12'
}

def releaseTime() {
    return new Date().format("MM_dd_hh_mm_ss", TimeZone.getTimeZone("GMT+08:00"))
}

